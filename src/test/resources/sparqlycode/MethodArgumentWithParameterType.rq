
 # Licensed to the Apache Software Foundation (ASF) under one or more
 # contributor license agreements.  See the NOTICE file distributed with
 # this work for additional information regarding copyright ownership.
 # The ASF licenses this file to You under the Apache License, Version 2.0
 # (the "License"); you may not use this file except in compliance with
 # the License.  You may obtain a copy of the License at
 # 
 #      http://www.apache.org/licenses/LICENSE-2.0
 # 
 # Unless required by applicable law or agreed to in writing, software
 # distributed under the License is distributed on an "AS IS" BASIS,
 # WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 # See the License for the specific language governing permissions and
 # limitations under the License.


# MethodArgumentWithParameterType checks that a method has an argument with a particular parameter type declaration
# Paul Worrall, Interition Ltd.

PREFIX sc:   <http://www.interition.net/sparqlycode/id/>
PREFIX java: <http://ontology.interition.net/java/ref/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX subject: <http://www.interition.net/sparqlycode/id/net/interition/sparqlycode/testsuite/>

ASK WHERE {

     SELECT (COUNT(?typeArgument) AS ?number)  
          WHERE  {  
          	subject:GenericMethodExercise java:method ?method .
          	# we are interested in the subtyped method
            ?method rdfs:label "subtyped" .
            # and its arguments
            ?method java:argument ?argument .
            
            # those arguments which have a type of List with a type parameter
            ?argument java:type <http://www.interition.net/sparqlycode/id/java/util/List> ;
            	 java:typeParameter ?typeParameter .
            
            # get the type parameters argument
            ?typeParameter java:argument ?typeArgument .
            # it should be a wildecard with an extend bound on Number
            ?typeArgument rdfs:label "?" ; java:extends  <http://www.interition.net/sparqlycode/id/java/lang/Number> .
          }
     HAVING ( ?number = 1 )

}
